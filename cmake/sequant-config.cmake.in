# - CMAKE Config file for the SeQuant package
# This will define the following CMake cache variables
#
#    SEQUANT_FOUND        - true if SeQuant library were found
#    SEQUANT_VERSION      - the x.y.z SeQuant version
#    SEQUANT_EXT_VERSION  - the x.y.z(-buildid) SeQuant version, where the (optional) buildid is smth like beta.3
#
# and the following imported targets
#
#     SeQuant::SeQuant    - the SeQuant library
#

# Set package version
set(SEQUANT_VERSION "@SEQUANT_VERSION@")
set(SEQUANT_EXT_VERSION "@SEQUANT_EXT_VERSION@")

@PACKAGE_INIT@

@Boost_CONFIG_FILE_CONTENTS@

set(SEQUANT_HAS_TILEDARRAY @SEQUANT_HAS_TILEDARRAY@)
if(SEQUANT_HAS_TILEDARRAY AND NOT TARGET tiledarray)
  set(TiledArray_CONFIG @TiledArray_CONFIG@)
  if (NOT TiledArray_CONFIG OR NOT EXISTS ${TiledArray_CONFIG})
    message(FATAL_ERROR "Expected TiledArray config file at ${TiledArray_CONFIG}; directory moved since SeQuant configuration?")
  endif()
  get_filename_component(TiledArray_DIR ${TiledArray_CONFIG} DIRECTORY)
  find_dependency(TiledArray CONFIG QUIET REQUIRED COMPONENTS tiledarray PATHS ${TiledArray_DIR} NO_DEFAULT_PATH)
endif()

set(SEQUANT_HAS_BTAS @SEQUANT_HAS_BTAS@)
if(SEQUANT_HAS_BTAS AND NOT TARGET BTAS::BTAS)
  set(BTAS_CONFIG @BTAS_CONFIG@)
  if (NOT BTAS_CONFIG OR NOT EXISTS ${BTAS_CONFIG})
    message(FATAL_ERROR "Expected BTAS config file at ${BTAS_CONFIG}; directory moved since SeQuant configuration?")
  endif()
  get_filename_component(BTAS_DIR ${BTAS_CONFIG} DIRECTORY)
  find_dependency(BTAS CONFIG QUIET REQUIRED PATHS ${BTAS_DIR} NO_DEFAULT_PATH)
endif()

set(SEQUANT_HAS_EIGEN @SEQUANT_HAS_EIGEN@)
if (NOT TARGET Eigen3::Eigen AND SEQUANT_HAS_EIGEN)
  if (TARGET TiledArray_Eigen)
     add_library(Eigen3::Eigen ALIAS TiledArray_Eigen)
  else()
    get_filename_component(Eigen3_DIR "@Eigen3_CONFIG@" DIRECTORY)
     # re:NO_CMAKE_PACKAGE_REGISTRY: eigen3 registers its *build* tree with the user package registry ...
     #                               to avoid issues with wiped build directory look for installed eigen
     find_package(Eigen3 3.0 CONFIG REQUIRED NO_CMAKE_PACKAGE_REGISTRY HINTS "${Eigen3_DIR}")
  endif()
endif()

if (NOT TARGET range-v3::range-v3)
  get_filename_component(range-v3_DIR "@range-v3_CONFIG@" DIRECTORY)
  find_dependency(range-v3 QUIET REQUIRED HINTS "${range-v3_DIR}")
endif(NOT TARGET range-v3::range-v3)

if (NOT TARGET libperm::libperm)
  get_filename_component(libperm_DIR "@libperm_CONFIG@" DIRECTORY)
  find_dependency(libperm QUIET REQUIRED HINTS "${libperm_DIR}")
endif(NOT TARGET libperm::libperm)

if (NOT TARGET polymorphic_variant::polymorphic_variant)
  get_filename_component(polymorphic_variant_DIR "@polymorphic_variant_CONFIG@" DIRECTORY)
  find_dependency(polymorphic_variant QUIET REQUIRED HINTS "${polymorphic_variant_DIR}")
endif(NOT TARGET polymorphic_variant::polymorphic_variant)

if (NOT TARGET Threads::Threads)
    find_dependency(Threads REQUIRED)
endif (NOT TARGET Threads::Threads)

# Include library IMPORT targets
if(NOT TARGET SeQuant::SeQuant)
  include("${CMAKE_CURRENT_LIST_DIR}/sequant-targets.cmake")
  if(NOT TARGET SeQuant::SeQuant)
    message(FATAL_ERROR "expected SeQuant::SeQuant among imported SeQuant targets")
  endif()
endif()

set(SEQUANT_FOUND TRUE)
